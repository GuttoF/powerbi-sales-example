DEFINE
    MEASURE Metricas[FaturamentoItem] = SUMX(fVendas, fVendas[Qtde] * fVendas[Vl.Unitario])

    MEASURE Metricas[Faturamento] = SUMX(fVendas, [FaturamentoItem])
    MEASURE Metricas[Meta] = SUM(fMetas[Vl.Meta])
    MEASURE Metricas[FreteSum] = SUM(fVendas[Vl.Frete])
    MEASURE Metricas[DataMax] = MAX(dCalendario[Data.Pedido])
    MEASURE Metricas[RankProduto] = RANKX(ALL(dProdutos[Cod.Produto]), [Faturamento], , DESC, DENSE)

    MEASURE Metricas[% Faturamento Total] = DIVIDE([Faturamento], CALCULATE([Faturamento], ALL(fVendas)))
    MEASURE Metricas[% Vendas Meta] = DIVIDE([Faturamento], [Meta])
    MEASURE Metricas[% Frete] = DIVIDE([FreteSum], [Faturamento])
    MEASURE Metricas[% Faturamento Produto] = DIVIDE([Faturamento], CALCULATE([Faturamento], ALL(dProdutos)))

    MEASURE Metricas[% Var MoM] = 
    VAR FaturamentoAtual = [Faturamento]
    VAR FaturamentoAnterior = [FaturamentoMoM]
    RETURN 
        IF(
            NOT ISBLANK(FaturamentoAtual) && NOT ISBLANK(FaturamentoAnterior),
            (FaturamentoAtual / FaturamentoAnterior) - 1,
            BLANK()
        )

    
    MEASURE Metricas[% Var YoY] =
    VAR ValorAtual = [Faturamento]
    VAR ValorAnterior = [FaturamentoYoY]
    RETURN
        IF(
            NOT ISBLANK(ValorAtual) &&
            NOT ISBLANK(ValorAnterior) &&
            ValorAnterior <> 0,
            (ValorAtual / ValorAnterior) - 1,
            BLANK()
        )

    MEASURE Metricas[QtdeClientes] = DISTINCTCOUNT(fVendas[Cod.Cliente])
    MEASURE Metricas[QtdePedidos] = DISTINCTCOUNT(fVendas[Pedido])
    MEASURE Metricas[QtdeVendas] = COUNTROWS(fVendas)
    MEASURE Metricas[QtdeProdutosVendidos] = DISTINCTCOUNT(fVendas[Cod.Produto])

    MEASURE Metricas[Ticket Medio] = DIVIDE([Faturamento], [QtdePedidos])
    MEASURE Metricas[Ticket Medio Cliente] = DIVIDE([Faturamento], [QtdeClientes])
    MEASURE Metricas[Ticket Medio Mensal] = 
    AVERAGEX(
        VALUES(dCalendario[Mês]),
        [Ticket Medio]
    )


    MEASURE Metricas[VarMeta] = [% Vendas Meta] - 1

    MEASURE Metricas[FaturamentoMTD] = TOTALMTD([Faturamento], dCalendario[Data.Pedido])
    MEASURE Metricas[FaturamentoQTD] = TOTALQTD([Faturamento], dCalendario[Data.Pedido])
    MEASURE Metricas[FaturamentoYTD] = TOTALYTD([Faturamento], dCalendario[Data.Pedido])
    MEASURE Metricas[FaturamentoMoM] = CALCULATE([Faturamento], DATEADD(dCalendario[Data.Pedido], -1, MONTH)) 
    MEASURE Metricas[FaturamentoYoY] = CALCULATE([Faturamento], SAMEPERIODLASTYEAR(dCalendario[Data.Pedido]))
    
    MEASURE Metricas[FaturamentoAcumulado] =
    CALCULATE(
        [Faturamento],
        FILTER(
            ALL(dCalendario[Data.Pedido]),
            dCalendario[Data.Pedido] <= MAX(dCalendario[Data.Pedido])
        )
    )

    MEASURE Metricas[FaturamentoSubTotal] = DIVIDE([Faturamento], CALCULATE([Faturamento], ALLSELECTED(fVendas)))

    MEASURE Metricas[FaturamentoBimestre] =
    VAR DataFinal = MAX(dCalendario[Data.Pedido])
    VAR DataInicial = EDATE(DataFinal, -1)
    RETURN
        CALCULATE(
            [Faturamento],
            FILTER(
                ALL(dCalendario[Data.Pedido]),
                dCalendario[Data.Pedido] >= DataInicial &&
                dCalendario[Data.Pedido] <= DataFinal
            )
        )

    -- Estatísticas
    MEASURE Metricas[FaturamentoMin] = MINX(fVendas, [FaturamentoItem])
    MEASURE Metricas[FaturamentoMax] = MAXX(fVendas, [FaturamentoItem])
    MEASURE Metricas[FaturamentoMedia] = AVERAGEX(fVendas, [FaturamentoItem])
    MEASURE Metricas[FaturamentoMediana] = MEDIANX(fVendas, [FaturamentoItem])

    MEASURE Metricas[MediaMovel3M] = AVERAGEX(
    DATESINPERIOD(dCalendario[Data.Pedido], MAX(dCalendario[Data.Pedido]), -3, MONTH),
    [Faturamento]
    )

    MEASURE Metricas[DiasComVenda] = 
    CALCULATE(
        DISTINCTCOUNT(dCalendario[Data.Pedido]),
        fVendas
    )